! Initial condition for the vector potential.
! This file contains the expression used for the initial 
! vector potential. You can use temporary real arrays R1-R2 
! of size (n,jsta:jend), and temporary complex arrays C1-C5 
! of size (n,ista:iend) to do intermediate computations. 
! The variable a0 should control the global amplitude of the 
! initial condition, and variables aparam0-9 can be used to 
! control the amplitudes of individual terms. At the end, the 
! potential in spectral space should be stored in the array 
! az (plus bz for the z-component of the magnetic field in 
! 2.5D solvers).

! Superposition of harmonic modes with random phases
! (vector potential and magnetic field, 2.5D)
!     a0  : amplitude of the 2D field
!     mkdn: minimum wave number
!     mkup : maximum wave number
!     aparam0: amplitude of the z component

      IF (ista.eq.1) THEN
         az(1,1) = 0.0_GP
         bz(1,1) = 0.0_GP
         DO j = 2,n/2+1
            IF ((ka2(j,1).le.mkup**2).and.(ka2(j,1).ge.mkdn**2)) THEN
               phase = 2*pi*randu(seed)
               az(j,1) = (COS(phase)+im*SIN(phase))/sqrt(ka2(j,1))
               az(n-j+2,1) = conjg(az(j,1))
               phase = 2*pi*randu(seed)
               bz(j,1) = (COS(phase)+im*SIN(phase))/sqrt(ka2(j,1))
               bz(n-j+2,1) = conjg(bz(j,1))
            ELSE
               az(j,1) = 0.0_GP
               az(n-j+2,1) = 0.0_GP
               bz(j,1) = 0.0_GP
               bz(n-j+2,1) = 0.0_GP
            ENDIF
         END DO
         DO j = 1,n
            DO i = 2,iend
               IF ((ka2(j,i).le.mkup**2).and.(ka2(j,i).ge.mkdn**2)) THEN
                  phase = 2*pi*randu(seed)
                  az(j,i) = 2*(COS(phase)+im*SIN(phase))/sqrt(ka2(j,i))
                  phase = 2*pi*randu(seed)
                  bz(j,i) = 2*(COS(phase)+im*SIN(phase))/sqrt(ka2(j,i))
               ELSE
                  az(j,i) = 0.0_GP
                  bz(j,i) = 0.0_GP
               ENDIF
            END DO
         END DO
      ELSE
         DO j = 1,n
            DO i = ista,iend
               IF ((ka2(j,i).le.mkup**2).and.(ka2(j,i).ge.mkdn**2)) THEN
                  phase = 2*pi*randu(seed)
                  az(j,i) = 2*(COS(phase)+im*SIN(phase))/sqrt(ka2(j,i))
                  phase = 2*pi*randu(seed)
                  bz(j,i) = 2*(COS(phase)+im*SIN(phase))/sqrt(ka2(j,i))
               ELSE
                  az(j,i) = 0.0_GP
                  bz(j,i) = 0.0_GP
               ENDIF
            END DO
         END DO
      ENDIF
      CALL normalize(az,a0,1,MPI_COMM_WORLD)
      CALL normalize(bz,aparam0,2,MPI_COMM_WORLD)
      IF ((corr.gt.tiny).and.(u0.gt.tiny)) THEN
         az = corr*sqrt(a0)*ps/sqrt(u0)+sqrt(1.0GP-corr**2)*az
      ENDIF
      IF ((corr.gt.tiny).and.(vparam0.gt.tiny)) THEN
         bz = corr*sqrt(aparam0)*bz/sqrt(vparam0)+sqrt(1.0GP-corr**2)*bz
      ENDIF
